# -*- mode: sh -*-
#
# My current uber minimal prompt has been stripped from the dizzying heights of
# multi-line oh-my-zsh/zprezto/starship based, distracting indicator laden
# nonsense to a single semi-colon and now I feel more at peace with the world.
function prompt_martin_setup {
  prompt off
  setopt LOCAL_OPTIONS
  setopt PROMPT_SUBST
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)

  # Bump on an SSH indicator on for those rare occasions where I'm signed in
  # to another box.
  (( ${+SSH_CONNECTION} )) && prompt_host="$(hostname) "

  # Set xterm style prompt titles.
  autoload -Uz add-zsh-hook
  add-zsh-hook -Uz chpwd (){print -Pn "\e]2;%2~\a"}

  # Tell prezto we manage this prompt.
  zstyle ':prezto:module:prompt' managed 'yes'

  # Directory tracking for Emacs vterm.
  vterm_printf() { printf "\e]%s\e\\" "$1" }
  vterm_prompt_end() { vterm_printf "51;A$(whoami)@$(hostname):$(pwd)"; }
  PROMPT='%F{2}${prompt_host}%(?.%(!.#.;).%F{1}%B;%b%f) '
  PROMPT=$PROMPT'%{$(vterm_prompt_end)%}'
}
prompt_martin_setup "$@"
